; Example PICA200 vertex shader

; Uniforms
	.fvec projection[4]
	.fvec tint
	.fvec depthinfo

	; Constants
	.constf myconst(0.0, 1.0, -1.0, 0.1)
	.constf myconst2(-50.0, -50.0, -50.0, -50.0)
	.alias  zeros myconst.xxxx ; Vector full of zeros
	.alias  ones  myconst.yyyy ; Vector full of ones
	
	; Outputs
	.out outpos position
	.out outclr color
	.out outtc0 texcoord0
	
	; Inputs (defined as aliases for convenience)
	.alias inpos v0
	.alias intc v1
	
	.proc main
		; Force the w component of inpos to be 1.0
		mov r0.xyz, inpos
		mov r0.w,   ones

		mul r5.x, depthinfo.x, r0.z
		add r0.x, r0.x, r5.x
	
		; outpos = projectionMatrix * inpos
		dp4 outpos.x, projection[0], r0
		dp4 outpos.y, projection[1], r0
		dp4 outpos.z, projection[2], r0
		dp4 outpos.w, projection[3], r0

		mov r2, depthinfo

		; r3 = Z - min_depth
		add r3, r0.zzzz, -r2.yyyy

		; r5 = (Z - min_depth) / deepness
		rcp r5, r2.wwww
		mul r5, r5.xxxx, r3
		; add r5, myconst.zzzz, r5

		mov outclr.rgb, r5.rgb
		mov outclr.a, ones

		mov outtc0, intc
		
		end
	.end